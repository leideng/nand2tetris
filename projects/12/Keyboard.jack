// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Keyboard.jack

/**
 * A library for handling user input from the keyboard.
 */
class Keyboard {

    /** Initializes the keyboard. */
    function void init() 
    {
        return;
    } 

    /**
     * Returns the ASCII code (as char) of the currently pressed key,
     * or 0 if no key is currently pressed.
     * Recognizes all ASCII characters, as well as the following extension
     * of action keys:
     * New line = 128 = String.newline()
     * Backspace = 129 = String.backspace()
     * Left Arrow = 130
     * Up Arrow = 131
     * Right Arrow = 132
     * Down Arrow = 133
     * Home = 134
     * End = 135
     * Page Up = 136
     * Page Down = 137
     * Insert = 138
     * Delete = 139
     * ESC = 140
     * F1 - F12 = 141 - 152
     */
    function char keyPressed() 
    {
        return Memory.peek(24576);
    }

    /**								
     * Reads the next character from the keyboard.
     * waits until a key is pressed and then released, then echoes
     * the key to the screen, and returns the value of the pressed key.
     */
    function char readChar() 
    {
        var char c;
        var char key;
        var bool ok;
        
        let ok = false;
        let key = 0;
        do Output.printChar(0); //display the cursor as a black block
        while(~ok)
        {
            //detect key-pressed
            while(key = 0)
            {
                let key = Keyboard.keyPressed();
            }
            let c = key;
            
            //detect key-released
            while(~(key = 0))
            {
                let key = Keyboard.keyPressed();
            }
            
            do Output.backSpace(); 
            do Output.printChar(c);
            
            let ok = true;
        }
        
        return c;
    }

    /**								
     * Prints the message on the screen, reads the next line
     * (until a newline character) from the keyboard, and returns its value.
     */
    function String readLine(String message) 
    {
        var String s;
        var String new_s;
        var int current_capacity;
        var char c;
        var bool ok;
        var int i;
        
        let current_capacity = 16; //initial capacity
        let s = String.new(current_capacity);
        let ok = false;
        
        //print the message
        do Output.printString(message);
        
        while(~ok)
        {
            let c = Keyboard.readChar();
            if(c = String.newLine())
            {
                do Output.backSpace(); 
                do Output.printChar(c); //print newline
                let ok = true;
            }
            else
            {
                if(c = String.backSpace())
                {
                    do s.eraseLastChar();
                }
                else //not backspace not newline
                {                    
                    if(s.length() = current_capacity) //we need to increase the capacity of the string
                    {
                        let current_capacity = current_capacity*2; //double the capacity
                        let new_s = String.new(current_capacity);
                        let i = 0; 
                        while(i < s.length())
                        {
                            do new_s.appendChar(s.charAt(i));
                            let i = i + 1;
                        }
                        do s.dispose();
                        let s = new_s;
                        let s = s.appendChar(c);
                    }
                    else
                    {
                        let s = s.appendChar(c);
                    }
                }
            }
        }
        
        return s;
    }   

    /**								
     * Prints the message on the screen, reads the next line
     * (until a newline character) from the keyboard, and returns its
     * integer value (until the first non numeric character).
     */
    function int readInt(String message) 
    {
        var String s;
        var int ret;
        
        let s = Keyboard.readLine(message);
        let ret = s.intValue();
        
        return ret;
    }
}
